buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
        jcenter()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails:grails-docs:$grailsVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'io.github.groovylang.groovydoc:groovydoc-gradle-plugin:1.0.1'
        classpath "gradle.plugin.org.aim42:htmlSanityCheck:$htmlSanityCheckVersion"

        classpath "io.micronaut.docs:micronaut-docs-asciidoc-extensions:$micronautDocsVersion"
        classpath "io.micronaut.docs:micronaut-docs-gradle-plugins:$micronautDocsVersion", {
            exclude module: 'shadow'
            exclude module: 'plugin-shadow'
        }
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'com.github.hierynomus.license' version '0.15.0' apply false
    id 'com.github.ben-manes.versions' version '0.28.0'
    id "com.github.johnrengelman.shadow" version "4.0.2" apply false
    id "net.ltgt.apt-eclipse" version "0.18" apply false
    id "net.ltgt.apt-idea" version "0.18" apply false
}

apply plugin: 'org.aim42.htmlSanityCheck'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://oss.jfrog.org/oss-snapshot-local" }
    maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion

ext {
    distInstallDir = file("$buildDir/dist-tmp")
    homeBinDir = file("bin")
    homeLibDir = file("lib")
    homeSrcDir = file("src")
}

subprojects { Project subproject ->
    group "io.micronaut"
    version project.projectVersion

    ext {
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
        isConfiguration = subproject.projectDir.parentFile.name == "configurations"
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://oss.jfrog.org/oss-snapshot-local" }
        maven { url "https://repo.grails.org/grails/core" }
        jcenter()
    }

    apply plugin: "groovy"
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    dependencyManagement {
        imports {
            mavenBom "io.micronaut:micronaut-bom:$micronautVersion"
        }
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    compileJava.options.compilerArgs.add '-parameters'
    compileTestJava.options.compilerArgs.add '-parameters'

    jar {
        manifest {
            attributes('Automatic-Module-Name': "${subproject.group}.${subproject.name}".replaceAll('[^\\w\\.\\$_]', "_"))
            attributes('Implementation-Version': projectVersion)
            attributes('Implementation-Title': 'Micronaut')
        }
    }

    tasks.withType(Test) {
        jvmArgs '-Duser.country=US'
        jvmArgs '-Duser.language=en'
        testLogging {
            exceptionFormat = 'full'
        }

        afterSuite {
            System.out.print(".")
            System.out.flush()
        }

        reports.html.enabled = !System.getenv("TRAVIS")
        reports.junitXml.enabled = !System.getenv("TRAVIS")
    }

    configurations {
        documentation
        all {
            resolutionStrategy.eachDependency { DependencyResolveDetails details ->
                String group = details.requested.group
                if (group == 'org.codehaus.groovy') {
                    details.useVersion(groovyVersion)
                }
                if (group == "org.ow2.asm") {
                    details.useVersion(asmVersion)
                }
            }
        }
    }

    dependencies {
        annotationProcessor("io.micronaut:micronaut-inject-java:${micronautVersion}")
        annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:$micronautDocsVersion"
        compile("io.micronaut:micronaut-http:${micronautVersion}")
        compile("io.micronaut:micronaut-http-server:${micronautVersion}")
        compile("io.micronaut:micronaut-http-server-netty:${micronautVersion}")
        compile("io.micronaut:micronaut-inject:${micronautVersion}")
        compileOnly("io.micronaut:micronaut-inject-java:${micronautVersion}")

        compile "org.shredzone.acme4j:acme4j-client:${acmeVersion}"
        compile "org.shredzone.acme4j:acme4j-utils:${acmeVersion}"


        documentation "org.codehaus.groovy:groovy-templates:${groovyVersion}"
        documentation "org.codehaus.groovy:groovy-dateutil:${groovyVersion}"

        compile group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: '2.0.25.Final'

        testCompile "org.testcontainers:spock:1.13.0"
        testCompile("io.micronaut:micronaut-http-client:${micronautVersion}")
        testCompile("org.codehaus.groovy:groovy-dateutil:$groovyVersion")
        testCompile("org.spockframework:spock-core:${spockVersion}") {
            exclude(group: "org.codehaus.groovy", module: "groovy-all")
        }
        testCompile "cglib:cglib-nodep:${cglibVersion}"
        testCompile "io.micronaut:micronaut-inject-groovy:${micronautVersion}"
        testCompile "io.micronaut.test:micronaut-test-spock:${micronautTestVersion}"
        testCompile "org.codehaus.groovy:groovy-test:${groovyVersion}"
        testCompile "org.objenesis:objenesis:${objenesisVersion}"
        testRuntime "ch.qos.logback:logback-classic:${logbackClassicVersion}"
    }

    groovydoc {
        classpath += project.configurations.documentation
    }

    task allDeps(type: DependencyReportTask) {}

    apply plugin: "checkstyle"
    checkstyle {
        toolVersion = project.checkstyleToolVersion
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")

        // Per submodule
        maxErrors = 1
        maxWarnings = 10

        showViolations = true
    }

    checkstyleTest.enabled = false
}

apply from: "https://raw.githubusercontent.com/micronaut-projects/micronaut-docs/v${micronautDocsVersion}/gradle/docs.gradle"

allprojects {
    apply plugin: 'idea'

    idea {
        module {
            outputDir file('build/classes/java/main')
            testOutputDir file('build/classes/groovy/test')
        }
    }
    apply from: rootProject.file('gradle/license.gradle')

    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
        forkEvery = 100
    }
}

htmlSanityCheck {
    sourceDir = new File("${rootProject.buildDir}/docs/")
    sourceDocuments = ["index.html"]

}

docs.finalizedBy(htmlSanityCheck)

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
